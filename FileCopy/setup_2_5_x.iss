; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!


; ВНИМАНИЕ!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
; Новые комментарии ВНАЧАЛЕ ФАЙЛА
;
;10-12-2018  == Ivanov
;возможность отката на пред версию 
;бэкап всего при установке 
; Компиляция файла выполнялась в Inno Setup 5.6.1
;
;29-08-2018 
; videovlc перенесен сразу в modules
;
;06-08-2018  == Serov
; переименован обратно modules\base_topcprim.psm -> modules\topc_prim.psm
;
;09-07-2018  == Serov
; переименован modules\topc_prim.psm -> modules\base_topcprim.psm
;15-06-2018 == Ivanov
; KIOHelper, NetPlugin
;
;15-02-2018 == Ivanov
; Sounds and Sounds_old
;
;18-12-2017 == Ivanov
; Бэкап настроек во время инсталяции перенесен в папку  ASUD Scada\Backup\Setup
;
;
;25-07-2017 == Ivanov
;FileDownloadService - убран из сборки
;БЭКАП настроек перенесен в CurStepChanged
;Часть Exec переделано на SW_HIDE
;Убрал Информацию о необходимости перезагрузки после установки
;
;
;02-05-2017 == Ivanov
; По окончании процесса установки Сервер автоматически регистрируется
;
; 23-03-2017 == Ivanov
; AsudBase.exe убран из \Scada 
; Остается только в папке \OPC Server
;
;
; ВНИМАНИЕ!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
; При добавлении нового пакета VCRedist или MFCRT обновить функции IsVCRedistNeed...
; 15-11-2012 убран RestartReplace  отовсюду  кроме тех что копируются в WinSys
; 22-02-2013 -  ==Ivanov
; убрано копирование чего бы то нибыло в WinSys (extgui32u.dll и т.п.) - т.к. может быть конфликт,
; например с remotejournal при пересборке версий.
; Firebird 2.1 ставиться / неставиться автоматически - тут может быть гипотетическая проблема если стоит Firebird 1.5
; 2.1 не поставиться и не напишет никакой ошибки как сейчас "Запущена копия Firebird"
; Драйвер ключа ставиться / не ставиться автоматически - при обновлении следует убрать проверку.
; 08-07-2013  ==Ivanov
; Драйверы pult.drv kio.drv ставятся по умолчанию в папку !drivers_old
; Убрал выбор драйверов вообще - все ставятся по умолчанию
; 16-10-2013  ==Ivanov
; в связи с изменением политики работы драйверов (будем брать за это доп деньги) через КЦС-IP
; данные модули убраны из дистрибутива.
; 30-01-2014  ==Ivanov
; Добавлена тестовая карта settings/map.conf
; 04-02-2014  ==Ivanov
; Add kcs_driver.psm opcconn.psm
; 16-06-2014  ==Ivanov
; haspdinst.exe копируется в папку ASUD Scada \ Ext
; 25-06-2014  ==Ivanov
; kio2mcheck.psm копируется в папку Scada \ modules  - Временно убрал (01.08.2014)
; 05-11-2014  ==Ivanov
; scada\modules\kio2mcheck.psm
; scada\modules\control.psm
; 18-11-2014  ==Ivanov
; scada\modules\smsterminal.psm
; 09-12-2014  ==Ivanov
; Server\modules\conc_table_view.psm
; 09-02-2015  ==Ivanov
; Scada\modules\videojpeg.psm
; 10-02-2015  ==Serov
; Папка установки изменена на "C:\1Tekon\ASUD Scada", закомментирована установка mfccrt
; 02-04-2015  ==Serov
; drivers_old\kio_driver.psm удален из сборки
; 24-06-2015  ==Ivanov
; modules\zinfo.psm добавлен
; 04-09-2015  ==Ivanov
; из установки исключена папка Docs
; 16-11-2015  ==Ivanov
; Delphi-plugins Modules-addon
; 12-01-2016  ==Ivanov
; Файл opcsrv.exe - не удаляется, а сервер - разрегистрируется
; map.conf - не удаляется при UnInstall
; delphi модули в сборке
; Убрал XP - хасп драйверы
; 25-01-2016
; Добавлено создание BackupSettings для Server and Scada при установке
; 11-02-2016
; Добавлено программа Архивный журнал A_JOURNAL
; 04-05-2016
; Восстановил XP - хасп драйверы
; 20-09-2016
; modules\fix.psm  variables.psm - добавлены


#define MyAppVer "2.5.3";
#define MyAppVer_ StringChange(MyAppVer, ".", "_")
#define DistribSource "distributive_2_5_x"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{E00425DD-7DB6-4C19-BF23-04EB86952B1E}
AppName=ASUD SCADA v{#MyAppVer}
AppVerName=ASUD SCADA - Диспетчер v{#MyAppVer}
AppPublisher=ООО "Текон-Автоматика"
AppPublisherURL=http://www.tekon.ru
AppSupportURL=http://www.tekon.ru
AppUpdatesURL=http://www.tekon.ru
DefaultDirName=C:\1Tekon\ASUD Scada
DefaultGroupName=Текон ASUD Scada
OutputDir=s:\!_tekon software
OutputBaseFilename=setup_{#MyAppVer_}
Compression=lzma
SolidCompression=true
SourceDir=s:\!_tekon software\{#DistribSource}
VersionInfoCompany=Tekon Automatic
VersionInfoProductName=Tekon SCADA
VersionInfoProductVersion={#MyAppVer}
; Внимание! VersionInfoVersion Используется в SoftVer
VersionInfoVersion={#MyAppVer}
AlwaysRestart=no
InfoAfterFile=readme.txt
InfoBeforeFile=s:\!_tekon software\{#DistribSource}\install_before.txt
DisableDirPage=false

[Languages]
Name: russian; MessagesFile: compiler:Languages\Russian.isl

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}

[Types]
Name: Server; Description: Tekon OPC сервер
Name: Client; Description: Tekon SCADA
Name: Custom; Description: Выборочная установка; Flags: iscustom

[Components]
Name: cis; Description: Базовые компоненты; Flags: fixed; Types: Client Server Custom
Name: client; Description: Tekon SCADA; Types: Client
Name: server; Description: Tekon OPC сервер; Types: Server;

; УКАЗЫВАЕМ ЧТО УДАЛИТЬ НЕПОСРЕДСТВЕННО ПЕРЕД УСТАНОВКОЙ ЗДЕСЬ
[InstallDelete]
Type: filesandordirs ; Name: "{app}\SCADA\drivers"
Type: filesandordirs ; Name: "{app}\SCADA\extensions"
Type: filesandordirs ; Name: "{app}\SCADA\modules"
Type: filesandordirs ; Name: "{app}\SCADA\styles"
Type: files; Name: "{app}\SCADA\*.dll"
Type: files; Name: "{app}\SCADA\*.exe"
; unins000 надо удалить иначе хранится старый деинсталлятор
Type: files; Name: "{app}\unins000.*";
Type: filesandordirs ; Name: "{app}\OPC Server\drivers"
Type: filesandordirs ; Name: "{app}\OPC Server\extensions"
Type: filesandordirs ; Name: "{app}\OPC Server\ip-tools"
Type: filesandordirs ; Name: "{app}\OPC Server\modules"
Type: filesandordirs ; Name: "{app}\OPC Server\styles"
Type: files; Name: "{app}\OPC Server\*.exe"
Type: files; Name: "{app}\OPC Server\*.dll"
Type: files; Name: "{app}\SCADA\modules\tadd_prim.psm"
Type: files; Name: "{app}\SCADA\asudbase.exe"
Type: files; Name: "{app}\OPC Server\db\testudf.dll"

[Delete]


[Files]
;
; ==================== client ====================
; divers

Source: drivers\h323\yealink.drv; DestDir: {app}\SCADA\drivers\h323; Components: client; Flags:   ignoreversion 32bit
Source: drivers\h323.psm; DestDir: {app}\SCADA\drivers; Components: client; Flags:   ignoreversion 32bit
Source: drivers\h323_ex.psm; DestDir: {app}\Scada\drivers; Components: client; Flags:   ignoreversion 32bit
; extensions

;Source: s:\distributive_2_3_x\extensions\basic_module.fx; DestDir: {app}\SCADA\extensions; Components: client; Flags:   ignoreversion 32bit

Source: extensions\map.fx; DestDir: {app}\SCADA\extensions; Components: client; Flags:   ignoreversion 32bit
Source: extensions\opc_core.fx; DestDir: {app}\SCADA\extensions; Components: client; Flags:   ignoreversion 32bit
Source: extensions\lvm_core.fx; DestDir: {app}\SCADA\extensions; Components: client; Flags:   ignoreversion 32bit
Source: extensions\opcda_core.fx; DestDir: {app}\SCADA\extensions; Components: client; Flags:   ignoreversion 32bit
Source: extensions\opcda_tekadapt.fx; DestDir: {app}\SCADA\extensions; Components: client; Flags:   ignoreversion 32bit
Source: extensions\exttools.fx; DestDir: {app}\SCADA\extensions; Components: client; Flags:   ignoreversion 32bit
Source: extensions\object_mgr.fx; DestDir: {app}\SCADA\extensions; Components: client; Flags:   ignoreversion 32bit
Source: extensions\bootstrap-4\css\*.*; DestDir: {app}\SCADA\extensions\bootstrap-4\css; Components: client; Flags:   ignoreversion 32bit
Source: extensions\bootstrap-4\js\*.*; DestDir: {app}\SCADA\extensions\bootstrap-4\js; Components: client; Flags:   ignoreversion 32bit
Source: tools-scada\*.*; DestDir: {app}\Scada\tools-scada; Components: client; Flags: uninsneveruninstall ignoreversion   32bit

; layout
; modules
; Source: s:\distributive_2_3_x\extensions\dump.fx; DestDir: {app}\SCADA\extensions; Components: client; Flags:   ignoreversion 32bit
Source: modules\h323_callmgr.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\journal.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\extprim_container.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\topc_prim.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\security.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\preport.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\ssaver.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\vjm.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\opjournal.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\sound_notify.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\itv.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\phonebook.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\zinfo.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\opc_signal.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\control.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\web.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\jmail.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\template.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\mapstat.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\datalogger.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\servplug.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\tprim.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\variables.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\fix.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\pgsclear.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\h323check.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\backup.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\smsterminal.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\kiohelper.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\netplugin.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\textfunc.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\videovlc.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
Source: modules\alarmclock.psm; DestDir: {app}\SCADA\modules; Components: client; Flags:   ignoreversion 32bit
;Source: modules_addon\*.*; DestDir: {app}\SCADA\modules_addon; Components: client; Flags:   ignoreversion 32bit

; A_JOURNAL
Source: modules\journal.psm; DestDir: {app}\A_JOURNAL\modules; Components: client; Flags: uninsneveruninstall  ignoreversion 32bit
Source: modules\vjm.psm; DestDir: {app}\A_JOURNAL\modules; Components: client; Flags:  uninsneveruninstall ignoreversion 32bit
Source: modules\ajournal.psm; DestDir: {app}\A_JOURNAL\modules; Components: client; Flags: uninsneveruninstall  ignoreversion 32bit
Source: styles\default.theme; DestDir: {app}\A_JOURNAL\styles; Components: client; Flags: uninsneveruninstall  ignoreversion 32bit
Source: fbclient.dll; DestDir: {app}\A_JOURNAL; Components: client; Flags: uninsneveruninstall ignoreversion   32bit
Source: extgui32u.dll; DestDir: {app}\A_JOURNAL; Components: client; Flags: uninsneveruninstall ignoreversion  32bit
Source: a_journal.exe; DestDir: {app}\A_JOURNAL; Components: client; Flags: uninsneveruninstall ignoreversion  32bit

; sounds
Source: sounds\*.*; DestDir: {app}\Scada\sounds; Components: client; Flags: ignoreversion   32bit
Source: sounds_old\*.*; DestDir: {app}\Scada\sounds_old; Components: client; Flags: ignoreversion   32bit

; styles
Source: styles\default.theme; DestDir: {app}\SCADA\styles; Components: client; Flags:   ignoreversion 32bit
; main

;Scripts
Source: scripts\lvm\lvm_entry.lua; DestDir: {app}\SCADA\scripts\lvm; Components: client; Flags:   ignoreversion
Source: scripts\lvm\built-in\*; DestDir: {app}\SCADA\scripts\lvm\built-in; Components: client; Flags:   ignoreversion recursesubdirs
Source: scripts\lvm\custom\entry.lua; DestDir: {app}\SCADA\scripts\lvm\custom; Components: client; Flags: onlyifdoesntexist
Source: scripts\text\*.*; DestDir: {app}\SCADA\scripts\text; Components: client; Flags: onlyifdoesntexist

Source: gsm0610_ptplugin.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit
Source: fbclient.dll; DestDir: {app}\SCADA; Components: client; Flags: ignoreversion   32bit
Source: scada.exe; DestDir: {app}\SCADA; Components: client; Flags: ignoreversion   32bit

Source: extluabind32u.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit

Source: extopal32n.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit
Source: extpt32n.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit

Source: extlua32u.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit
Source: extwx32u.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit
Source: extwxstc32u.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit
Source: extwxlua32u.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit
Source: extLuaBind32u.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit
Source: exttolua32u.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit

Source: extboostdatetime32u.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit
Source: extboostregex32u.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit
Source: extboostsystem32u.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit
Source: extboostthread32u.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit
Source: extboosttimer32u.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit

Source: settings\map.conf; DestDir: {app}\Scada\settings; Components: client; Flags: onlyifdoesntexist uninsneveruninstall 32bit
Source: scada-no-splash.bat; DestDir: {app}\Scada; Components: client; Flags: onlyifdoesntexist 32bit
Source: scada-dump.bat; DestDir: {app}\Scada; Components: client; Flags: onlyifdoesntexist 32bit

;
; =================== server ====================
;
Source: extopal32n.dll; DestDir: {app}\OPC Server; Components: server; Flags:   ignoreversion 32bit
Source: extpt32n.dll; DestDir: {app}\OPC Server; Components: server; Flags:   ignoreversion 32bit

Source: extboostdatetime32u.dll; DestDir: {app}\OPC Server; Components: server; Flags:   ignoreversion 32bit
Source: extboostregex32u.dll; DestDir: {app}\OPC Server; Components: server; Flags:   ignoreversion 32bit
Source: extboostsystem32u.dll; DestDir: {app}\OPC Server; Components: server; Flags:   ignoreversion 32bit
Source: extboostthread32u.dll; DestDir: {app}\OPC Server; Components: server; Flags:   ignoreversion 32bit
Source: extboosttimer32u.dll; DestDir: {app}\OPC Server; Components: server; Flags:   ignoreversion 32bit

Source: gsm0610_ptplugin.dll; DestDir: {app}\SCADA; Components: client; Flags:   ignoreversion 32bit
Source: fbclient.dll; DestDir: {app}\OPC Server; Components: server; Flags: ignoreversion   32bit
Source: asudbase.exe; DestDir: {app}\OPC Server; Components: server; Flags: uninsneveruninstall ignoreversion   32bit
Source: tools-server\*.*; DestDir: {app}\OPC Server\tools-server; Components: server; Flags: uninsneveruninstall ignoreversion   32bit
Source: kcslogger.exe; DestDir: {app}\OPC Server; Components: server; Flags: uninsneveruninstall ignoreversion   32bit
Source: db\*.*; DestDir: {app}\OPC Server\db; Components: server; Flags: uninsneveruninstall ignoreversion   32bit
Source: settings\kscip.ini; DestDir: {app}\OPC Server\settings; Components: server; Flags: onlyifdoesntexist  uninsneveruninstall  32bit
Source: settings\general.conf; DestDir: {app}\OPC Server\settings; Components: server; Flags: onlyifdoesntexist uninsneveruninstall 32bit
Source: settings\general.conf; DestDir: {app}\SCADA\settings; Components: client; Flags: onlyifdoesntexist uninsneveruninstall 32bit
Source: settings\template_guard.conf; DestDir: {app}\OPC Server\settings; Components: client; Flags: onlyifdoesntexist uninsneveruninstall 32bit
Source: settings\template_termor.conf; DestDir: {app}\OPC Server\settings; Components: client; Flags: onlyifdoesntexist uninsneveruninstall 32bit
Source: settings\template_temperature.conf; DestDir: {app}\OPC Server\settings; Components: client; Flags: onlyifdoesntexist uninsneveruninstall 32bit
Source: settings\template_liner.conf; DestDir: {app}\OPC Server\settings; Components: client; Flags: onlyifdoesntexist uninsneveruninstall 32bit
Source: settings\template_analog.conf; DestDir: {app}\OPC Server\settings; Components: client; Flags: onlyifdoesntexist uninsneveruninstall 32bit
Source: settings\template_sensors.conf; DestDir: {app}\OPC Server\settings; Components: client; Flags: onlyifdoesntexist uninsneveruninstall 32bit
Source: settings\template_control.conf; DestDir: {app}\OPC Server\settings; Components: client; Flags: onlyifdoesntexist uninsneveruninstall 32bit
Source: !unreg_opc.bat; DestDir: {app}\OPC Server; Components: server; Flags: onlyifdoesntexist 32bit

; drivers
Source: drivers\h323.psm; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit
Source: drivers\ll_driver.psm; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit
Source: drivers\rslayer.psm; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit
Source: drivers\iprs_driver.psm; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit
Source: drivers\pult_driver.psm; DestDir: {app}\OPC Server\drivers_old; Components: server; Flags:   ignoreversion 32bit
Source: drivers\usbpult_driver.psm; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit
Source: drivers\kio2m_driver.psm; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit
Source: drivers\wacon.psm; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit; Check: IsWACONNeed; 
Source: drivers\wacon.psm; DestDir: {app}\OPC Server\drivers_old; Components: server; Flags:   ignoreversion 32bit
Source: drivers\tekoncdc.inf; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit
Source: drivers\kcs_driver.psm; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit
Source: drivers\h323_ex.psm; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit
Source: drivers\kcslogger.psm; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit
Source: drivers\rsdev_ext.psm; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit
Source: drivers\hwdev_ext.psm; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit
Source: drivers\network.psm; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit
Source: drivers\opcconn2.psm; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit
Source: drivers\dbmonitor.psm; DestDir: {app}\OPC Server\drivers; Components: server; Flags:   ignoreversion 32bit

; Убрано 16-10-2013
Source: extensions\kcsip.dll; DestDir: {app}\OPC Server\extensions; Components: server; Flags:  uninsneveruninstall  ignoreversion 32bit
Source: extensions\drv\*.*; DestDir: {app}\OPC Server\extensions\drv; Components: server; Flags: uninsneveruninstall  ignoreversion 32bit

; Для работы опросчика ТС ВИС.Т
Source: !_shared\hydralink.dll; DestDir: {app}\OPC Server; Components: server; Flags:  ignoreversion 32bit
Source: !_shared\pcomm.dll; DestDir: {app}\OPC Server; Components: server; Flags:  ignoreversion 32bit
Source: !_shared\hydralink.dll; DestDir: {app}\OPC Server\tools-server; Components: server; Flags:  ignoreversion 32bit
Source: !_shared\pcomm.dll; DestDir: {app}\OPC Server\tools-server; Components: server; Flags:  ignoreversion 32bit



; extensions
Source: extensions\topcda.fx; DestDir: {app}\OPC Server\extensions; Components: server; Flags:   ignoreversion 32bit
Source: extensions\topcda_drvmgr.fx; DestDir: {app}\OPC Server\extensions; Components: server; Flags:   ignoreversion 32bit

; layout
; modules
Source: modules\asudbase.psm; DestDir: {app}\OPC Server\modules; Components: server; Flags:   ignoreversion 32bit
Source: modules\h323_callmgr.psm; DestDir: {app}\OPC Server\modules; Components: server; Flags:   ignoreversion 32bit
Source: modules\hwlayer.psm; DestDir: {app}\OPC Server\modules; Components: server; Flags:   ignoreversion 32bit
Source: modules\opcconfig.psm; DestDir: {app}\OPC Server\modules; Components: server; Flags:   ignoreversion 32bit
Source: modules\oscwindow.psm; DestDir: {app}\OPC Server\modules; Components: server; Flags:   ignoreversion 32bit
Source: modules\security.psm; DestDir: {app}\OPC Server\modules; Components: server; Flags:   ignoreversion 32bit
Source: modules\preport.psm; DestDir: {app}\OPC Server\modules; Components: server; Flags:   ignoreversion 32bit
Source: modules\opcconn.psm; DestDir: {app}\OPC Server\modules; Components: server; Flags:   ignoreversion 32bit
Source: modules\conc_table_view.psm; DestDir: {app}\OPC Server\modules; Components: server; Flags:   ignoreversion 32bit
Source: modules\template_conc.psm; DestDir: {app}\OPC Server\modules; Components: server; Flags:   ignoreversion 32bit
Source: modules\kiohelper.psm; DestDir: {app}\OPC Server\modules; Components: server; Flags:   ignoreversion 32bit
Source: modules\netplugin.psm; DestDir: {app}\OPC Server\modules; Components: server; Flags:   ignoreversion 32bit

; styles
Source: styles\default.theme; DestDir: {app}\OPC Server\styles; Components: server; Flags:   ignoreversion 32bit
; main
Source: opcsrv.exe; DestDir: {app}\OPC Server; Components: server; Flags: ignoreversion   32bit
;
; ============= firebird =====================
;

Source: !_shared\fbsetup.exe; DestDir: {tmp}; Components: server client; Flags: deleteafterinstall ignoreversion 32bit; Check: IsFBNeed;

;
; ============= LUA shared files =============
;

;
; ============= Readme.txt =============
;
Source: Readme.txt; DestDir: {app}\; Components: server client; Flags: ignoreversion 32bit
Source: help\*; DestDir: {app}\Help; Components: server client; Flags: ignoreversion 32bit


;
; ============= OPC, MFC, CRT shared files =====================
;

;Source: s:\distributive_2_3_x\!_shared\mfccrt.msi; DestDir: {app}\Ext; Components: cis server client; Flags: ignoreversion 32bit
Source: !_shared\opccore.msi; DestDir: {tmp}; Components: client server; Flags: deleteafterinstall ignoreversion 32bit
Source: !_shared\vcredist_x86.exe; DestDir: {app}\Ext; Components: client server; Flags: uninsneveruninstall ignoreversion 32bit

; NOTE: Don't use "Flags: ignoreversion" on any shared system files
; убрал для extgui32u sharedfile 08-08-2011
Source: extgui32u.dll; DestDir: {app}\OPC Server; Components: server; Flags: ignoreversion  32bit
Source: extgui32u.dll; DestDir: {app}\Scada; Components: client; Flags: ignoreversion  32bit

; ====
; HASP
Source: haspdinst.exe; DestDir: {app}\Ext; Flags: uninsneveruninstall ignoreversion 32bit; Components: client
Source: hasp_srm_xpe_runtime_install\aksrepo\akshasp.cat; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP
Source: hasp_srm_xpe_runtime_install\aksrepo\akshhl.cat; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP
Source: hasp_srm_xpe_runtime_install\aksrepo\aksusb.cat; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP
Source: hasp_srm_xpe_runtime_install\aksrepo\akshhl27.dll; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP
Source: hasp_srm_xpe_runtime_install\aksrepo\akshsp51.dll; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP
Source: hasp_srm_xpe_runtime_install\aksrepo\aksusb3.dll; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP
Source: hasp_srm_xpe_runtime_install\aksrepo\hasplms.exe; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP
Source: hasp_srm_xpe_runtime_install\aksrepo\akshasp.inf; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP
Source: hasp_srm_xpe_runtime_install\aksrepo\akshhl.inf; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP
Source: hasp_srm_xpe_runtime_install\aksrepo\aksusb.inf; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP
Source: hasp_srm_xpe_runtime_install\aksrepo\aksclass.sys; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP
Source: hasp_srm_xpe_runtime_install\aksrepo\aksfridge.sys; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP
Source: hasp_srm_xpe_runtime_install\aksrepo\akshasp.sys; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP
Source: hasp_srm_xpe_runtime_install\aksrepo\akshhl.sys; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP
Source: hasp_srm_xpe_runtime_install\aksrepo\aksusb.sys; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP
Source: hasp_srm_xpe_runtime_install\aksrepo\hardlock.sys; DestDir: {sys}; Flags: restartreplace; Components: client; Check : IsHASPNeedXP

[Icons]
Name: {group}\Текон ASUD Scada; Filename: {app}\SCADA\scada.exe; Components: client; IconIndex: 0; Flags: uninsneveruninstall
Name: {group}\Настройка OPC-сервера; Filename: {app}\OPC Server\opcsrv.exe; Components: server; IconIndex: 0; Flags: uninsneveruninstall
Name: {group}\Удаление ПО; Filename: {uninstallexe}

; desktop
Name: {commondesktop}\Текон ASUD Scada; Filename: {app}\SCADA\scada.exe; Tasks: desktopicon; Components: client; IconIndex: 0; Flags: uninsneveruninstall
Name: {commondesktop}\Настройка OPC-сервера; Filename: {app}\OPC Server\opcsrv.exe; Tasks: desktopicon; Components: server; IconIndex: 0; Flags: uninsneveruninstall

[Run]
;Filename: {app}\Ext\mfccrt.msi; Parameters: /passive; Flags: shellexec waituntilterminated; Components: cis server client; Check: IsMFCNeed
Filename: {app}\Ext\vcredist_x86.exe; Parameters: /Q; Flags: waituntilterminated; Components: client server; Check: IsVCREdistNeed
Filename: {tmp}\opccore.msi; Parameters: /passive; Flags: shellexec waituntilterminated; Components: server client;  Check : IsOPCNeed
Filename: {app}\Ext\haspdinst.exe; Parameters: -install -nomsg; Flags: waituntilterminated; Components: client; Check: IsHASPNeed;
Filename: {tmp}\fbsetup.exe; Parameters: /silent; Flags: waituntilterminated; Components: server client ; Check: IsFBNeed;

;Filename: {app}\OPC Server\opcsrv.exe; Description: Настроить OPC-сервер; Components: server; Flags: postinstall nowait unchecked
;Filename: {app}\SCADA\scada.exe; Description: Запустить ASUD Scada; Components: client; Flags: postinstall nowait unchecked

[UninstallRun]
Filename: {app}\OPC Server\opcsrv.exe; Parameters: -s -u --stop --silent; Flags: waituntilterminated; Components: server
Filename: {app}\CIS\tcis.exe; Parameters: -s -u --stop --silent; Flags: waituntilterminated; Components: cis

[Registry]
Root: HKLM; Subkey: "SOFTWARE\Tekon\SCADA"; ValueType: string; ValueName: "Path"; ValueData: "{app}";
Root: HKLM; Subkey: "SOFTWARE\Tekon\SCADA"; ValueType: string; ValueName: "Version"; ValueData: {code:SoftVer};
Root: HKLM; Subkey: "System\CurrentControlSet\Control\Lsa";  ValueType: dword;  ValueName : "ForceGuest";  ValueData: 0
Root: HKLM; Subkey: "System\CurrentControlSet\Control\Lsa";  ValueType: dword;  ValueName : "EveryoneIncludesAnonymous";  ValueData: 0

[_ISTool]
UseAbsolutePaths=true

[Code]

var
    CURRENT_INSTALLED_VERSION : String;
    ProgressPage: TOutputProgressWizardPage;
    WACONNeed : Boolean;

const
    TH32CS_SNAPPROCESS = $2;
    INVALID_HANDLE_VALUE = -1;

    V_BACKUP = 'Version_BACKUP';
    
    strEventLog = 'eventcreate /T WARNING /ID 1 /L Tekon.Scada-Audit /SO Installer /D "%s"';

type
    TPROCESSENTRY32 = record
        dwSize, cntUsage, th32ProcessID: DWORD;
        th32DefaultHeapID: Longint;
        th32ModuleID, cntThreads, th32ParentProcessID: DWORD;
        pcPriClassBase: Longint;
        dwFlags: DWORD;
        szExeFile: array [0..259] of char;
    end;
    

function CreateToolhelp32Snapshot(dwFlags, th32ProcessID: DWORD): THandle; external 'CreateToolhelp32Snapshot@kernel32.dll stdcall';
function Process32First(hSnapshot: THandle; var lppe: TPROCESSENTRY32): Boolean; external 'Process32First@kernel32.dll stdcall';
function Process32Next(hSnapshot: THandle; var lppe: TPROCESSENTRY32): Boolean; external 'Process32Next@kernel32.dll stdcall';
function CloseHandle(hObject: THandle): Boolean; external 'CloseHandle@kernel32.dll stdcall';

function IsProcessRunning(FileName: String): Boolean; //FileName - имя exe-файла процесса
var
    hProcessSnap: THandle;
    pe32: TPROCESSENTRY32;
    szExeFile: String;
begin
    Result := False; 
    hProcessSnap := CreateToolhelp32Snapshot(TH32CS_SNAPPROCESS, 0);
    if hProcessSnap = INVALID_HANDLE_VALUE then Exit;
    pe32.dwSize := sizeof(pe32);
    if not Process32First(hProcessSnap, pe32) then Exit;
    while (not Result) and Process32Next(hProcessSnap, pe32) do
    begin
        szExeFile := '';
        while not (pe32.szExeFile[Length(szExeFile)] = #0) do  szExeFile := szExeFile + pe32.szExeFile[Length(szExeFile)];
        Result := LowerCase(FileName) = LowerCase(szExeFile);
    end;
    CloseHandle(hProcessSnap);
end;

function GetFileVersion(const FileName: string): string;
var
  Version: string;
begin
  Result:= '0.0.0.0';
  if GetVersionNumbersString(FileName, Version) then
  Result:= Version;
end;

function SoftVer (Param : String): String;
var 
 s : String;
begin
  s :=  '{#SetupSetting("VersionInfoVersion")}';
  Result := s;
  Exit;
  // Версия файлов не всегда отражала то что ставится
  S := ExpandConstant ('{app}');
  Result := GetFileVersion (S + '\Scada\scada.exe');
  if Result = '' then GetFileVersion (S +'\OPC Server\opcsrv.exe');
end;

// COMPARE FILE VERSIONS  --------------------------------------------------------------------

function Count(What, Where: String): Integer;
begin
   Result := 0;
    if Length(What) = 0 then
        exit;
    while Pos(What,Where)>0 do
    begin
        Where := Copy(Where,Pos(What,Where)+Length(What),Length(Where));
        Result := Result + 1;
    end;
end;

//split text to array
procedure Explode(var ADest: TArrayOfString; aText, aSeparator: String);
var tmp: Integer;
begin
    if aSeparator='' then
        exit;

    SetArrayLength(ADest,Count(aSeparator,aText)+1)

    tmp := 0;
    repeat
        if Pos(aSeparator,aText)>0 then
        begin

            ADest[tmp] := Copy(aText,1,Pos(aSeparator,aText)-1);
            aText := Copy(aText,Pos(aSeparator,aText)+Length(aSeparator),Length(aText));
            tmp := tmp + 1;

        end else
        begin

             ADest[tmp] := aText;
             aText := '';

        end;
    until Length(aText)=0;
end;

//compares two version numbers, returns -1 if vA is newer, 0 if both are identical, 1 if vB is newer
function CompareVersion(vA,vB: String): Integer;
var tmp: TArrayOfString;
    verA,verB: Array of Integer;
    i,len: Integer;
begin
    Explode(tmp,vA,'.');
    SetArrayLength(verA,GetArrayLength(tmp));
    for i := 0 to GetArrayLength(tmp) - 1 do
        verA[i] := StrToIntDef(tmp[i],0);

    Explode(tmp,vB,'.');
    SetArrayLength(verB,GetArrayLength(tmp));
    for i := 0 to GetArrayLength(tmp) - 1 do
        verB[i] := StrToIntDef(tmp[i],0);

    len := GetArrayLength(verA);
    if GetArrayLength(verB) < len then
        len := GetArrayLength(verB);

    for i := 0 to len - 1 do
        if verA[i] < verB[i] then
        begin
            Result := 1;
            exit;
        end else
        if verA[i] > verB[i] then
        begin
            Result := -1;
            exit
        end;

    if GetArrayLength(verA) < GetArrayLength(verB) then
    begin
        Result := 1;
        exit;
    end else
    if GetArrayLength(verA) > GetArrayLength(verB) then
    begin
        Result := -1;
        exit;
    end;

    Result := 0;
end;

// Функция проверки версии vcredist
function GetLibVersion (DirSubStr, DllName : String) : String;
var
 FindRec1 : TFindRec;
 fv : String;
 fname : String;
begin
   Result := '0.0.0.0';
   if FindFirst(ExpandConstant('{win}\winsxs\*'), FindRec1) then begin
    try
      repeat
        // Don't count directories
        if (FindRec1.Attributes and FILE_ATTRIBUTE_DIRECTORY)  = FILE_ATTRIBUTE_DIRECTORY then
         begin
          if (pos (DirSubStr,FindRec1.Name) > 0) then
           begin
            fName := ExpandConstant('{win}\winsxs\')+FindRec1.Name + '\'+DllName;
            if FileExists(fName) then
             begin
               fv :=  GetFileVersion(fName);
               if (CompareVersion(fv,result)<0) then result := fv;
             end
           end
         end;
      until not FindNext(FindRec1);
    finally
      FindClose(FindRec1);
    end;
  end;
end;

// -----------------------


function IsOPCNeed () : Boolean;
var
 s : String;
begin
 S := ExpandConstant ('{sys}') + '\opcenum.exe';
 Result := not fileexists(S);
end;

function IsWACONNeed () : Boolean;
begin
 Result := Waconneed;
end;

function IsVCREdistNeed () : Boolean;
begin
 if (CompareVersion ('9.0.30729.1',GetLibVersion ('VC90.ATL','atl90.dll')) < 0)
  then REsult := True
  else Result := False;
end;

function IsMFCNeed () : Boolean;
begin
 Result := False;
 // по сути пакет vcredist содержит более новые файлы этого пакета.
 // если что-то поменяется сделать по аналогии с IsVCRedistNeed
end;

function IsFBNeed () : Boolean;
begin
 Result := not (IsProcessRunning ('fbserver.exe'));
end;


function IsHASPNeed () : Boolean;
begin
 Result := not IsProcessRunning ('hasplms.exe');
end;


function IsHASPNeedXP () : Boolean;
var
  Version: TWindowsVersion;
begin
 GetWindowsVersionEx(Version); 
 if (Version.Major < 6)
  then Result := true
  else Result := false;
end;


function IsDotNetInstalled () : Boolean;
begin
 Result := RegKeyExists(HKLM, 'Software\Microsoft\NET Framework Setup\NDP');
 if (not Result) then
 begin
    MsgBox('По окончании установки следует также загрузить и установить пакет DotNetFramework', mbError, MB_OK);
 end 
end;

procedure InitializeWizard();
begin
  //WizardForm.ComponentsList.Checked[3] := False;
  //WizardForm.ComponentsList.Checked[4] := False;
  ProgressPage := CreateOutputProgressPage('Регистрация файлов сообщений EventLog',
    '');
end;

Function Split(Expression: String; Separator: String): TArrayOfString;
Var
    i: Integer;
    tmpArray : TArrayOfString;
    curString : String;

Begin
    i := 0;
    curString := Expression;

    Repeat
        SetArrayLength(tmpArray, i+1);
        If Pos(Separator,curString) > 0 Then    Begin
            tmpArray[i] := Copy(curString, 1, Pos(Separator, curString)-1);
            curString := Copy(curString, Pos(Separator,curString) + Length(Separator), Length(curString));
            i := i + 1;
        End Else Begin
             tmpArray[i] := curString;
             curString := '';
        End;
    Until Length(curString)=0;

    Result:= tmpArray;
End;

function GetUPFolder : String;
var
  S : String;
  SS : TArrayOfString;
  i : Integer;
begin
  S := ExpandConstant ('{app}');
  SS := Split (S,'\');
  Result := '';
  for i := 0 to GetArrayLength(SS)-2 do
     Result := Result + SS [i] + '\';
  if Length (Result) > 0 then
   delete (Result, Length(Result),1);
end;


function GetBackUPFolder : String;
begin
  Result := GetUPFolder + '\_BackUPVersion\';
end;


procedure CurStepChanged(CurStep: TSetupStep);
var
 ResultCode : Integer;
 S : String;
 V : String;
 bckFolder : String;
 excludefilename : String;
begin
 if (CurStep=ssInstall) then  // Перед непосредственно установкой
 begin
    S := ExpandConstant ('{app}');

    if FileExists (S+'\OPC Server\drivers\wacon.psm') 
    then 
      Waconneed := True
    else 
      Waconneed := False;
 
    if FileExists (S+'\OPC Server\d_file.exe') and  IsProcessRunning('d_file.exe') then
     Exec ('cmd.exe', '/c "' + S+'\OPC Server\d_file.exe" /UnInstall /Silent','',SW_HIDE, ewWaitUntilTerminated, ResultCode);

    V :='/c ' + Format (strEventLog,['Установка версии: ' + '{#SetupSetting("VersionInfoVersion")}']);
    Exec('cmd.exe', V, '', SW_HIDE, ewWaitUntilTerminated, ResultCode);

    if (not DirExists(ExpandConstant ('{app}'))) then  Exit;
    
    MsgBox('Будет выполнено резервирование текущей версии ПО' + #13#10 +
           'В случае необходимости вы сможете выполнить быстрый откат, ' + #13#10 +
           'просто выполнив unins000.exe из папки 1Tekon\ASUD Scada', mbInformation, MB_OK);

    RegWriteStringValue (HKLM, 'SOFTWARE\Tekon\SCADA', V_BACKUP, CURRENT_INSTALLED_VERSION);

    excludefilename := GetShortName(GetUpFolder + '\exclude.txt');
    SaveStringToFile(excludefilename,
     '\log\' + #13#10
     'original.gdb' + #13#10
     '\records\' + #13#10
     '\sounds_old\' + #13#10
     '\backup\' + #13#10 +
     '\a_journal\' + #13#10 +
     '\tools-scada\' + #13#10 +
     '\tools-server\' + #13#10 +
     '\ext\' + #13#10 +
     '\updates\' + #13#10 +
     '\extensions\drv\' + #13#10 +
     'kcsip.dll' + #13#10 +
     'kcslogger.exe' + #13#10 +
     'asudbase.exe' + #13#10 +
     'unins000' + #13#10
     , False);
     
    bckFolder := GetBackUPFolder;
    V  := '/c xcopy "' + S + '" "' + bckFolder+ '" /S /Y /Exclude:'+excludefilename;
    Exec ('cmd.exe', V,'',SW_SHOWNORMAL, ewWaitUntilTerminated, ResultCode);

    DeleteFile (excludefilename);
 end;
 if (CurStep=ssPostInstall) then     // Установка успешно завершена
 begin
    ProgressPage.SetText('SCADA (ожидайте ...)', '');
    ProgressPage.SetProgress(10, 100);
    ProgressPage.Show;
    try
    S := ExpandConstant ('{app}');
	
	 // удаляем ключи Layout 
    RegDeleteKeyIncludingSubkeys (HKCU,'Software\Tekon\PowerShell');
	
    Exec('cmd.exe', '/c "' + S + '\Scada\scada.exe" --log-install --no-splash --silent', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);

    ProgressPage.SetText ('OPC Server (ожидайте ...)', '');
    ProgressPage.SetProgress(60, 100);

    Exec('cmd.exe', '/c "' + S + '\OPC Server\opcsrv.exe" --log-install --no-splash --silent', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);

    ProgressPage.SetProgress(90, 100);

    Exec('cmd.exe', '/c "' + S + '\OPC Server\opcsrv.exe" -s --install --silent', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);

    ProgressPage.SetProgress(100, 100);

    finally
      ProgressPage.Hide;
    end;
 end;
 if (CurStep=ssDone) then           // Нажатие кнопки заверщение
 begin

 end;
end;

procedure CurUninstallStepChanged (CurUninstallStep: TUninstallStep);
var
  S         : String;
  V, VER         : String;
  bckFolder : String;
  ResultCode : Integer;
begin
 if Integer(CurUninstallStep)  = 3 then
 begin
    // Фиксирую событие в журнале событий
    V :='/c ' + Format (strEventLog,['Деинсталляция версии: ' + '{#SetupSetting("VersionInfoVersion")}']);
    Exec('cmd.exe', V, '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
 
 
    bckFolder := GetBackUPFolder;
    if not DirExists(bckFolder) then
    Exit;
    
    // восстанавливаем ключи реестра
    VER := '';
    RegQueryStringValue (HKLM, 'SOFTWARE\Tekon\SCADA', V_BACKUP, VER);
    RegDeleteValue      (HKLM, 'SOFTWARE\Tekon\SCADA', V_BACKUP);
    RegWriteStringValue (HKLM, 'SOFTWARE\Tekon\SCADA', 'Version', VER);

    // удаляем ключи Layout 
    RegDeleteKeyIncludingSubkeys (HKCU,'Software\Tekon\PowerShell');

    MsgBox('Выполняется восстановление предыдущей версии ПО: ' +VER +#13#10 +
           'Текущие Журналы событий и переговорной связи будут переименованы в *_uninst.db' + #13#10
           'В случае необходимости вы можете получить доступ к ним в программе A_Journal',
           mbInformation, MB_OK);

    V :='/c ' + Format (strEventLog,['Возврат версии: ' + VER]);
    Exec('cmd.exe', V, '', SW_HIDE, ewWaitUntilTerminated, ResultCode);

    if Length(bckFolder) > 0 then
      Delete (bckFolder,Length(bckFolder),1);
    S := ExpandConstant ('{app}')+'\';

    // на всякий случай переименовываем файлы journal, vjm
    V := '/c move /Y "' + S + 'Scada\journal.db" "' + S + 'Scada\journal_uninst.db"';
    Exec ('cmd.exe', V,'',SW_SHOWNORMAL, ewWaitUntilTerminated, ResultCode);
    V := '/c move /Y "' + S + 'Scada\vjm.db" "' + S + 'Scada\vjm_uninst.db"';
    Exec ('cmd.exe', V,'',SW_SHOWNORMAL, ewWaitUntilTerminated, ResultCode);
    
    // восстановление файлов предыдущей версии
    V  := '/c xcopy "' + bckFolder + '" "' + S + '" /S /Y';
    Exec ('cmd.exe', V,'',SW_SHOWNORMAL, ewWaitUntilTerminated, ResultCode);
    V  := '/c rmdir /S /Q "' + bckFolder;
    Exec ('cmd.exe', V,'',SW_SHOWNORMAL, ewWaitUntilTerminated, ResultCode);

 end;
end;


function InitializeSetup(): Boolean;
var
 ResultCode : Integer;
begin
  if not IsAdminLoggedOn () then
  begin
    Result := False;
    MsgBox('Для выполнения установки необходимы права Администратора', mbError, MB_OK);
    Exit;
  end;
  
  RegQueryStringValue (HKLM, 'SOFTWARE\Tekon\SCADA', 'Version', CURRENT_INSTALLED_VERSION);

  IsDotNetInstalled ();

  Result := (IsProcessRunning('scada.exe') or  IsProcessRunning('asudbase.exe') or IsProcessRunning('kcslogger.exe') or IsProcessRunning('rclient.exe'))
  if Result then
   begin
    MsgBox('Необходимо закрыть программы SCADA, ASUDBase, KSCLogger, RClient', mbError, MB_OK);
    Result := False;
    Exit;
   end;

  if IsProcessRunning('opcsrv.exe') then
     Exec('cmd.exe', '/c net stop topcserver', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);
   
  if IsProcessRunning('d_file.exe') then
     Exec('cmd.exe', '/c net stop FileDownloadService', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);

  if IsProcessRunning('opcsrv.exe') then
   begin
      Result := False;
      MsgBox ('Необходимо остановить процесс OPC сервера: ' + IntToStr(ResultCode) +#13#10+
              'и повторить установку.', mbError, MB_OK);
     Exit;
   end;

   Result := True;
end;





































































